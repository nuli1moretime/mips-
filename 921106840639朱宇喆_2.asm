##########################################################################################
#程序名称：万年历
#程序员：朱宇
#最后更改时间：2022年4月7号
#功能描述：控制台输入您要查询那一年的日历，程序计算后将此年日历输出，可反复进行此操作
################################################################################
	.data
month:	.asciiz " 1 JAN 2 FEB 3 MAR 4 APR 5 MAY 6 JUN 7 JUL 8 AUG 9 SEP10 OCT11 NOV12 DEC"
day:	.asciiz " 1 2 3 4 5 6 7 8 910111213141516171819202122232425262728293031"
	.align 2
mthday:	.space 48
week:	.asciiz " S  M  T  W  T  F  S"
ans: 	.space 2208
	.text
main:	li $v0,5		#输入目标年数
	syscall
	jal runfou		#判断是否为闰年
	jal thisyear		#转入thisyear函数
jp:	la $t2,ans		#取缓冲区首地址
	jal setblank		#转入setblank函数
	li $s1,4
	li $a1,1
	li $a2,-8
jump1:	beqz $s1,jump3		#双层循环将12个月的日历信息存入缓冲区
	li $s0,3
	li $a3,0
	addi $a2,$a2,8		#x0+8
	addi $s1,$s1,-1
jump2:	beqz $s0,jump1
	rem $s2,$s2,7
	jal monthpoint		#转入monthpoint函数
	addi $a1,$a1,1		#月份+1
	addi $a3,$a3,24		#y0+24
	addi $s0,$s0,-1
	b jump2
jump3:  move $a0,$t2		
	li $v0,4
	syscall			#打印缓冲区
	b main			#重复实验操作
################################################################################
#函数名：runfou
#最后修改时间：4月7日
################################################################################
#功能描述：判断传入年份是否为闰年
#使用的寄存器: $v0:判断的年份
#	       $t1:判断的结果（1为是0为否）	
################################################################################
runfou:	move $s0,$v0		#$s0传递判断年份
	move $s1,$v0		#$s1传递判断年份
	li $t0,4
	div $s1,$t0		
	mfhi $t1
	bne $t1,$0,end0		#如果此年份不是4的整数倍转入end0
	li $t0,100
	move $s1,$s0
	div $s1,$t0
	mfhi $t1
	bne $t1,$0,end1		#如果此年不是100的整数倍转入end1
	li $t0,400
	move $s1,$s0
	div $s1,$t0
	mfhi $t1
	beq $t1,$0,end1		#如果此年是400的整数倍转入end1
	j end0
end0:	li,$t1,0		#此年不是闰年
	jr $ra
end1:	li $t1,1		#此年是闰年
	jr $ra
#################################################################################
#函数名：thisyear
#最后修改时间：4月7日
################################################################################
#功能描述：计算此年每月共有多少天并存入mthday缓冲区，并计算此年第一天为星期几
#使用寄存器：$v0:今夕何年  $t1:今年是不是闰年 $s2: 今年一月一号星期几
#		$s3:mthdy的首地址 
#################################################################################
thisyear:
	la $s3,mthday		#取mthday首地址
	li $s4,31
	li $s5,30
	li $s6,29
	sw $s4,($s3)		#将每月对应的天数存进mthday中
	sw $s4,8($s3)
	sw $s4,16($s3)
	sw $s4,24($s3)
	sw $s4,28($s3)
	sw $s4,36($s3)
	sw $s4,44($s3)
	sw $s5,12($s3)
	sw $s5,20($s3)
	sw $s5,32($s3)
	sw $s5,40($s3)
	beqz $t1,lp		
	sw $s6,4($s3)		#如果是闰年2月份有29天
	b lp2
lp:	addi $s6,$s6,-1		#如果不是闰年2月份有28天
	sw $s6,4($s3)
lp2:	li $s4,2022		
	beq $v0,$s4,lp9		#如果目标年数为2022转入lp9
	bgt $v0,$s4,lp3		#如果目标年大于2022转入lp3
	move $s5,$v0		#将大的存入$s6小的存入$s5
	move $s6,$s4
	b lp4
lp3:	move $s5,$s4
	move $s6,$v0
lp4:	li $s2,0
lp5:	beq $s5,$s6,lp8		#从小年循环加到大年
	move $v0,$s5		#判断此年是否为闰年
	jal runfou
	beqz $t1 lp6		#若不是闰年加365
	addi $s2,$s2,366	#若是闰年加366
	b lp7			
lp6:	addi $s2,$s2,365
lp7:    addi $s5,$s5,1
	b lp5			#$s2记录相隔天数
lp8:	li $s4,7
	remu $s2,$s2,$s4	#$s2和7取余
	addi $s2,$s2,6		
	ble $s2,$s4,ans1
	rem $s2,$s2,$s4		#算出此年的第一天是星期几
	b ans1
lp9:	li $s2,6
ans1:	j jp
#################################################################################
#函数名：setblank
#最后修改时间：4月7号
################################################################################
#程序功能：将缓冲区全部填上空格，在每一行的最后填上换行符
#使用寄存器： $t2: ans首地址   $t8：换行符  $t9：空格
################################################################################
setblank:
	move $t7,$t2
	li $t8,0x0a	#$t8为换行符
	li $t9,0x20	#$t9为空格
	li $s7,32
lop:	beqz $s7,ed
	li $s6,68
lop1:	beqz $s6,lop2	#循环存入32行
	sb $t9,($t7)	#循环存入68个空格
	addi $t7,$t7,1	
	addi $s6,$s6,-1
	b lop1
lop2:	sb $t8,($t7)	#存入一个换行符
	addi $t7,$t7,1
	addi $s7,$s7,-1
	b lop
ed:	jr $ra
#################################################################################
#函数名：monthpoint（n，x0，y0）
#最后修改时间：4月7号
################################################################################
#功能描述：在缓冲区（x0，y0）处存入n月份的日历信息
#使用寄存器：$a1:传递n $a2:传递x0 $a3:传递y0 
#		$s2:今年1月1日是星期几 $s4 先后为month/week/day的首地址 
#		$t2 ans首地址  $s3 mthday首地址
#################################################################################
monthpoint:
	la $s4,month		#取month首地址
	move $s6,$s4		#传递month首地址
	move $t4,$a1		#传递n打印月份
	li $s5,6
	addi $t4,$t4,-1		
	mulou $t4,$t4,$s5
	add $s6,$s6,$t4
	lb $s7,($s6)		#取出n月份对应的英文字符
	li $s5,69
	mulou $s5,$s5,$a2	#找出英文字符应填入的位置
	add $s5,$s5,$a3
	addi $s5,$s5,7
	add $s5,$s5,$t2		#算出距离首地址的偏移量
	sb $s7,($s5)		#将字符存入缓冲区
	li $t4,5
loop1:	beqz $t4 loop2
	addi $s6,$s6,1
	lb $s7,($s6)
	addi $s5,$s5,1
	sb $s7,($s5)
	addi $t4,$t4,-1
	b loop1
loop2:	addi $t1,$a2,1		#第二行打印week
	la $s4,week		#取week首地址
	li $s5,69
	mulou $s5,$s5,$t1
	add $s5,$s5,$a3
	add $s5,$s5,$t2		#算出偏移量打印week
	li $t8,20
loop3:  beqz $t8,loop4		#循环读写20次
	lb $s7,($s4)
	sb $s7,($s5)
	addi $s4,$s4,1
	addi $s5,$s5,1
	addi $t8,$t8,-1
	b loop3
loop4:	la $s4,day		#取day首地址
	addi $t1,$t1,1		#第三行开始打印日期
	li $t8,4
	addi $t9,$a1,-1
	mulou $t9,$t9,$t8
	add $t9,$t9,$s3
	lw $t8,($t9)		#计算这个月有多少天存入$t8
	li $t9,0
loop5:	beq $t9,$t8,end		#循环设置
	li $s5,69
	li $s6,7
	div $s2,$s6		
	mflo $t5		#$t5=$s2/7
	mfhi $t6		#$t6=$s2%7
	li $s6,3		
	add $t5,$t1,$t5		#$t5+=$a2
	mulou $s5,$t5,$s5	#$s5=69*(x0+$s2/7)
	mulou $t6,$t6,$s6	#$t6*=3
	add $s5,$s5,$a3		
	add $s5,$s5,$t6		#$s5+=y0+3*$s2%7
	add $s5,$s5,$t2		#计算偏移量
	li $t5,2
	mulou $t6,$t9,$t5	
	add $t6,$t6,$s4		#在day中找到相对应字符填入
	lb $s7,($t6)
	sb $s7,($s5)
	addi $t6,$t6,1
	addi $s5,$s5,1
	lb $s7,($t6)
	sb $s7,($s5)
	addi $s2,$s2,1		#计算下一天是星期几
	addi $t9,$t9,1		#记录进程
	b loop5
end:	jr $ra

